plugins {
    id "com.gradle.plugin-publish" version "1.2.1"
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id "idea"
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    api 'com.jcraft:jsch:0.1.55'
    api 'com.google.code.gson:gson:2.8.6'

    testImplementation('org.spockframework:spock-core:2.0-M4-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation('cglib:cglib-nodep:2.2')
    testImplementation('org.objenesis:objenesis:1.2')
    testImplementation gradleTestKit()
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

group = "edu.wpi.first"
version = "2024.2.0"

base {
    archivesName = "DeployUtils"
}

java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

gradlePlugin {
    website = 'https://github.com/wpilibsuite/deploy-utils'
    vcsUrl = 'https://github.com/wpilibsuite/deploy-utils'
    plugins {
        DeployUtils {
            id = 'edu.wpi.first.DeployUtils'
            displayName = 'DeployUtils'
            implementationClass = 'edu.wpi.first.deployutils.DeployUtils'
            description = 'Additions to the model-based DSL for deploying Java and Native projects to remote targets'
            tags = ['remote', 'target', 'deploy', 'java', 'native']
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:all,-missing', true)
}

def examplesFolder = file("$rootDir/testing")

tasks.register('PatchExamples') {
    doLast {
        String regex = "(id\\s*?[\\\"|\\']edu\\.wpi\\.first\\.DeployUtils[\\\"|\\'].*?version\\s*?[\\\"|\\'])(.+?)([\\\"|\\'])";

        examplesFolder.eachFile { File file ->
            if (file.isDirectory() && file.name != '_archived') {
                def buildGradleFile = new File(file, 'build.gradle')
                if (buildGradleFile.exists() && buildGradleFile.isFile()) {
                    def text = buildGradleFile.text
                    text = text.replaceAll(regex, "id \"edu.wpi.first.DeployUtils\" version \"${version}\"")
                    buildGradleFile.text = text
                }
            }
        }
    }
}

build.dependsOn PatchExamples
jar.finalizedBy PatchExamples

wrapper {
    gradleVersion = '8.5'
    distributionType = Wrapper.DistributionType.BIN
}
